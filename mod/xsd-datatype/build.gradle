plugins {
    id 'java-library'
}

group = 'com.thaiopensource'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':util')
    implementation project(':datatype')
    implementation project(':regex')
}

sourceSets {
    main {
        java {
            srcDir 'src/main'
        }
        resources {
            srcDir "build/generated/sources/resources/main"
        }
    }
    test {
        java {
            srcDir 'src/test'
        }
    }
}

tasks.register('generateServices') {
    def serviceFile = file("${buildDir}/generated/sources/resources/main/META-INF/services/org.relaxng.datatype.DatatypeLibraryFactory")

    outputs.file(serviceFile)

    doLast {
        serviceFile.parentFile.mkdirs()
        serviceFile.text = [
                'com.thaiopensource.datatype.xsd.DatatypeLibraryFactoryImpl',
        ].join('\n')
    }
}
processResources.dependsOn(generateServices)
