import groovy.xml.XmlSlurper

version = new XmlSlurper().parse(file('../version.xml')).text()

allprojects {
    version = rootProject.version
    group = 'com.thaiopensource'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'

    dependencies {
        testImplementation group: 'org.testng', name: 'testng', version: '7.8.0'
    }

    tasks.register('copyProperties', Copy) {
        from 'src/main'
        include '**/*.*'
        exclude '**/*.java'
        into 'build/resources/main'
    }
    processResources.dependsOn tasks.named('copyProperties')

    tasks.register('copyTestProperties', Copy) {
        from 'src/test'
        include '**/*.*'
        exclude '**/*.java'
        into 'build/resources/test'
    }
    processTestResources.dependsOn tasks.named('copyTestProperties')

    test.useTestNG()
}