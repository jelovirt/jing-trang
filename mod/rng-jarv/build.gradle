dependencies {
    implementation project(':util')
    implementation project(':resolver')
    implementation project(':xsd-datatype')
    implementation project(':rng-parse')
    implementation project(':pattern')
    implementation group: 'isorelax', name: 'isorelax', version: '20030108'
}

sourceSets {
    main {
        java {
            srcDir 'src/main'
        }
        resources {
            srcDir "build/generated/sources/resources/main"
        }
    }
    test {
        java {
            srcDir 'src/test'
        }
    }
}

jar {
    archiveFileName = "${project.name}.jar"
}

tasks.register('generateServices') {
    def services = [
            'org.iso_relax.verifier.VerifierFactoryLoader': [
                    'com.thaiopensource.relaxng.jarv.VerifierFactoryLoaderImpl',
            ],
    ]

    outputs.files services.collect { entry -> file("${buildDir}/generated/sources/resources/main/META-INF/services/${entry.key}") }

    doLast {
        services.each { entry ->
            {
                def serviceFile = file("${buildDir}/generated/sources/resources/main/META-INF/services/${entry.key}")
                serviceFile.parentFile.mkdirs()
                serviceFile.text = entry.value.join('\n')
            }
        }
    }
}
processResources.dependsOn tasks.named("generateServices")
