plugins {
    id 'java-library'
}

group = 'com.thaiopensource'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':util')
    implementation project(':resolver')
    implementation project(':datatype')
    implementation project(':rng-parse')
    implementation project(':pattern')
    implementation group: 'isorelax', name: 'isorelax', version: '20030108'
}

sourceSets {
    main {
        java {
            srcDir 'src/main'
        }
        resources {
            srcDir "build/generated/sources/resources/main"
        }
    }
    test {
        java {
            srcDir 'src/test'
        }
    }
}

tasks.register('generateServices') {
    def serviceFile = file("${buildDir}/generated/sources/resources/main/META-INF/services/org.iso_relax.verifier.VerifierFactoryLoader")

    outputs.file(serviceFile)

    doLast {
        serviceFile.parentFile.mkdirs()
        serviceFile.text = [
                'com.thaiopensource.relaxng.jarv.VerifierFactoryLoaderImpl',
        ].join('\n')
    }
}
processResources.dependsOn(generateServices)
